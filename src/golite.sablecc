Package golite;

Helpers
  all_chars = [0..0xffff];
  all_chars_but_linefeed = [all_chars-10];
  all_chars_but_star_slash = [[all_chars-'*']-'/'];
  star_without_slash = '*' [all_chars - '/'];
  tab = 9;
  line_feed = 10;
  carriage_return = 13;
  space = ' ';
  digit = ['0'..'9'];
  positive_digit = ['1'..'9'];
  integer = '0' | (positive_digit digit*);
  float_decimal = '.' digit*;
  letter = ['a'..'z'] | ['A'..'Z'];
  string_symbols = letter | digit | ':' | ':' | '~' | '#' | '$' | '%' | '^' | '&' | '*' | '-' | '+' | '/' | '`' | ''' | '<' | '>' | '=' | '_' | '|' | ';' | ':' | '{' | '}' | '[' | ']' | '(' | ')' | space | '@' | '!' | '?' | '.' | ',';
  escape_single_quote = '\' ''';
  escape_double_quote = '\"';
  escape_sequences = '\a' | '\b' | '\f' | '\n' | '\r' | '\t' | '\v' | '\\' | escape_single_quote;
  oct_digit = ['0' .. '7'] ;
  hex_digit = digit | ['A' .. 'F'] | ['a' .. 'f'] ;
  single_quote = ''' ;
  double_quote = '"' ;
  back_quote = '`' ;
  not_end_of_line = [all_chars - [carriage_return + line_feed]] ;

Tokens
  if = 'if';
  else = 'else';
  break = 'break';
  return = 'return';
  case = 'case';
  chan = 'chan';
  const = 'const';
  continue = 'continue';
  default = 'default';
  defer = 'defer';
  fallthrough = 'fallthrough';
  for = 'for';
  func = 'func';
  go = 'go';
  goto = 'goto';
  var = 'var';
  import = 'import';
  interface = 'interface';
  map = 'map';
  package = 'package';
  range = 'range';
  select = 'select';
  struct = 'struct';
  switch = 'switch';
  op_equals = ('+' | '&' | '&^' | '-' | '|' | '<' | '*' | '^' | '/' | '<<' | '%' | '>>') '=';
  type = 'float64'|'rune'|'int'|'bool'|'string';
  type_keyword = 'type';
  plus = '+';
  minus = '-';
  star = '*';
  slash = '/';
  modulo = '%';
  ampersand = '&';
  pipe = '|';
  caret = '^';
  shift_left = '<<';
  shift_right = '>>';
  ampersand_caret = '&^';
  logical_and = '&&';
  logical_or = '||';
  left_arrow = '<-';
  plus_plus = '++';
  minus_minus = '--';
  equals_equals = '==';
  less = '<';
  greater = '>';
  equals = '=';
  exclamation = '!';
  exclamation_equals = '!=';
  less_equals = '<=';
  greater_equals = '>=';
  colon_equals = ':=';
  dot_dot_dot = '...';
  l_paren = '(';
  r_paren = ')';
  l_brack = '[';
  r_brack = ']';
  l_brace = '{';
  r_brace = '}';
  comma = ',';
  semicolon = ';';
  colon = ':';
  dot = '.';
  print = 'print';
  println = 'println';
  append = 'append';
  comment = '//' all_chars_but_linefeed*;
  block_comment = '/*' (all_chars_but_star_slash | '/*' | star_without_slash)* '*/';
  int = integer;
  oct = '0' oct_digit+ ;
  hex = '0' ['x' + 'X'] hex_digit+;
  float64_literal = digit* '.' digit* | '.' digit+ ;
  //float64 = 'float64';
  //bool = 'bool';
  //rune = 'rune';
  rune_literal = single_quote ([not_end_of_line - [single_quote + '\']] | escape_single_quote | escape_sequences) single_quote ;
  raw_string_lit = back_quote [all_chars - back_quote]* back_quote ;
  interpreted_string_literal = double_quote ([not_end_of_line - [double_quote + '\']] | escape_double_quote| escape_sequences)* double_quote ;
  id = (letter | '_')(letter | digit | '_')*;
  end_of_file = 26;
  eol = line_feed | carriage_return | carriage_return line_feed;
  blank = tab | space;

Ignored Tokens
  eol, blank, comment, block_comment;

Productions
cst_prog {-> program} =
  {prog} cst_package_decl top_level_declarations* cst_block* {-> New program(cst_package_decl.package_decl, 
  [top_level_declarations.decl], [cst_block.stmt])}
  ; 

top_level_declarations{-> decl} = 
  {package_decl} cst_package_decl semicolon {-> New decl.pack_decl_ast(cst_package_decl.package_decl)} |
  {func_decl} cst_func_decl semicolon {-> New decl.func_decl_ast(cst_func_decl.func_decl)} |
  {declarations} declarations semicolon {-> declarations.decl}
  ;

cst_package_decl{-> package_decl} = 
  package cst_id semicolon {-> New package_decl(cst_id.id_type)}
  ;

declarations {-> decl} =
  {var_decl} cst_var_decl {-> New decl.var_decl_ast(cst_var_decl.var_decl)} |
  {type_decl} cst_type_decl {-> New decl.type_decl_ast(cst_type_decl.type_decl)}
  ;

//function declarations here
cst_func_decl{-> func_decl} = 
  {no_return} func cst_id l_paren multi_sig? r_paren cst_block 
  {-> New func_decl.no_return(cst_id.id_type, 
                           multi_sig.signature,
                           cst_block.stmt
        )} |
  {single_return} func [name]:cst_id l_paren multi_sig? r_paren cst_type cst_block 
  {-> New func_decl.single_return(name.id_type, 
                           multi_sig.signature,
                           cst_type.var_type,
                           cst_block.stmt
  )}
  ;

multi_sig {-> signature} = 
  {recurse} signature comma multi_sig {-> New signature.multiple_types(signature.signature, multi_sig.signature)} |
  {base} signature {-> signature.signature}
  ;

signature {-> signature} = 
  {recurse} cst_id comma signature {-> New signature.many_id_to_type(cst_id.id_type, signature.signature)} |
  {base} cst_id cst_type{-> New signature.single_id_to_type(cst_id.id_type, cst_type.var_type)}
  ;


//type declaration and necessary subproductions here
cst_type_decl {-> type_decl} =
  {single_line} type_keyword type_top_level{-> type_top_level.type_decl} |
  {multi_line} type_keyword l_paren type_top_level_eos* r_paren {-> New type_decl.multiline_list([type_top_level_eos.type_decl])}
  ;

type_top_level_eos {-> type_decl} =
  {type_top_level_eos} type_top_level cst_eos {-> type_top_level.type_decl};

type_top_level {-> type_decl} = 
  {alias} type_aliasing {-> type_aliasing.type_decl} |
  {struct} type_struct_declaration {-> type_struct_declaration.type_decl}
  ;

type_aliasing {-> type_decl} = 
  cst_id cst_type {-> New type_decl.type_alias(cst_id.id_type, cst_type.var_type)}
  ;
  
type_struct_declaration {-> type_decl} = 
  cst_id struct l_brace type_with_one_or_more_ids* r_brace {-> New type_decl.struct_with_id(cst_id.id_type, 
  [type_with_one_or_more_ids.type_decl])}
  ;

type_with_one_or_more_ids {-> type_decl} = 
  {recurse} cst_id comma type_with_one_or_more_ids {-> New type_decl.type_with_many_ids(cst_id.id_type, type_with_one_or_more_ids.type_decl)} |
  {base} cst_id cst_type semicolon {-> New type_decl.type_alias(cst_id.id_type, cst_type.var_type)}
  ;

//variable declaration and necessary subproductions here
cst_var_decl {-> var_decl} =
  {cst_single_line_decl} var top_var_decl {-> top_var_decl.var_decl} |
  {cst_multi_line_decl} var l_paren top_var_decl_eos* r_paren {-> New var_decl.multiline_list([top_var_decl_eos.var_decl])}
  ;

top_var_decl_eos {-> var_decl} =
  {cst_decl_eos}  top_var_decl semicolon {-> top_var_decl.var_decl};

top_var_decl {-> var_decl} = 
  {cst_miat} var_id_and_type {-> var_id_and_type.var_decl} |
  {cst_miae} var_id_and_exp_maybe_type {-> var_id_and_exp_maybe_type.var_decl}
  ;

var_id_and_type {-> var_decl} = 
  {miat_recurse} cst_id comma var_id_and_type {-> New var_decl.inline_list_no_exp(cst_id.id_type, var_id_and_type.var_decl)} |
  {miat_end} cst_id cst_type {-> New var_decl.var_with_type(cst_id.id_type, cst_type.var_type)}
  ;

var_id_and_exp_maybe_type {-> var_decl} = 
  {miae_recurse} cst_id [l]:comma var_id_and_exp_maybe_type [r]:comma cst_exp {-> New var_decl.inline_list_with_exp(cst_id.id_type, var_id_and_exp_maybe_type.var_decl, cst_exp.exp)} |
  {miae_end} cst_id equals cst_exp {-> New var_decl.var_with_only_exp(cst_id.id_type, cst_exp.exp)} |
  {mtae_end} cst_id cst_type equals cst_exp {-> New var_decl.var_with_type_and_exp(cst_id.id_type, cst_type.var_type, cst_exp.exp)};

//general type production for all declarations
cst_type {-> var_type} = 
  {cst_base_type} type {-> New var_type.base_type(type)}|
  {cst_slices} l_brack r_brack cst_type {-> New var_type.slice(cst_type.var_type)} | 
  {cst_arrays} l_brack int r_brack cst_type {-> New var_type.array(int, cst_type.var_type)}|
  {cst_structs} id {-> New var_type.struct(id)}
  ;
  
cst_id {-> id_type} = 
  {id} id {-> New id_type.id(id)} |
  {type} type {-> New id_type.type(type)}
  ;

cst_stmt {-> stmt} =
  {cst_empty}      /*empty*/           {-> New stmt.empty()} |
  {cst_decl}       declarations        {-> New stmt.decl(declarations.decl)} |
  {cst_simple}     cst_simple_stmt     {-> cst_simple_stmt.stmt} |
  {cst_print}      print l_paren cst_exp_list? r_paren {-> New stmt.print(cst_exp_list.exp)} |
  {cst_println}    println l_paren cst_exp_list? r_paren {-> New stmt.println(cst_exp_list.exp)} |
  {cst_return}     return cst_exp?  {-> New stmt.return(cst_exp.exp)} |
  {cst_if}         cst_if_stmt     {-> cst_if_stmt.stmt} |
  {cst_switch}     cst_switch_stmt {-> cst_switch_stmt.stmt} |
  {cst_for}        for cst_for_condition cst_block {-> New stmt.for(cst_for_condition.exp, cst_block.stmt)} |
  {cst_break}      break           {-> New stmt.break()} |
  {cst_continue}   continue        {-> New stmt.continue()};

cst_simple_stmt {-> stmt} =
  //{cst_empty}      /*empty*/           {-> New stmt.empty()} |
  //{cst_decl}       cst_top_decl        {-> New stmt.decl(cst_top_decl.decl)} |
  {cst_increment}  lvalue plus_plus    {-> New stmt.increment(lvalue.exp)} |
  {cst_decrement}  lvalue minus_minus  {-> New stmt.decrement(lvalue.exp)} |
  {cst_assign}     cst_assign_stmt     {-> cst_assign_stmt.stmt} |
  {cst_short_decl} cst_short_decl_stmt {-> cst_short_decl_stmt.stmt};

cst_return_stmt {-> } =
  {cst_empty}  {-> } |
  {cst_exp}   cst_exp {-> };

cst_if_stmt {-> stmt} = 
  {cst_if} if cst_exp cst_block cst_end_if? {-> New stmt.if(cst_exp.exp, cst_block.stmt, cst_end_if.stmt)};

cst_exp {-> exp} = 
  {factor}     factor {-> factor.exp} |
  {unary_op}   unary_op {-> unary_op.exp} |
  {binary_op}  binary_op {-> binary_op.exp} |
  {function_call} function_call {-> function_call.exp} |
  {append_expr} append_expr {-> append_expr.exp};

binary_op {-> exp} =
  {cst_plus}   cst_exp plus factor  {-> New exp.plus(cst_exp.exp,factor.exp)} |
  {cst_minus}  cst_exp minus factor {-> New exp.minus(cst_exp.exp,factor.exp)} | 
  {cst_logical_or} cst_exp logical_or factor {-> New exp.logical_or(cst_exp.exp, factor.exp)} |
  {cst_logical_and} cst_exp logical_and factor {-> New exp.logical_and(cst_exp.exp, factor.exp)} |
  {cst_pipe} cst_exp pipe factor {-> New exp.pipe(cst_exp.exp, factor.exp)} |
  {cst_caret} cst_exp caret factor {-> New exp.caret(cst_exp.exp, factor.exp)} |
  {cst_rel_op} rel_op {-> rel_op.exp};

rel_op {-> exp} = 
  {cst_equals_equals} cst_exp equals_equals factor {-> New exp.equals_equals(cst_exp.exp, factor.exp)} |
  {cst_not_equals} cst_exp exclamation_equals factor {-> New exp.not_equal(cst_exp.exp, factor.exp)} |
  {cst_less_than} cst_exp less factor {-> New exp.less(cst_exp.exp, factor.exp)} |
  {cst_greater_than} cst_exp greater factor {-> New exp.greater(cst_exp.exp, factor.exp)} |
  {cst_less_than_equals} cst_exp less_equals factor {-> New exp.less_equals(cst_exp.exp, factor.exp)} |
  {cst_greater_than_equals} cst_exp greater_equals factor {-> New exp.greater_equals(cst_exp.exp, factor.exp)};

unary_op {-> exp} = 
  {positive_expression} plus factor { -> New exp.unary_plus(factor.exp)} |
  {negative_expression} minus factor {-> New exp.unary_minus(factor.exp)} |
  {caret_expression} caret factor {-> New exp.unary_xor(factor.exp)} |
  {exclamation_exoression} exclamation factor {-> New exp.unary_exclamation(factor.exp)};

function_call {-> exp} =
  {empty_args} id l_paren r_paren {-> New exp.function_call(id, New exp.empty())} |
  {one_or_more_args} id l_paren cst_exp_list r_paren {-> New exp.function_call(id, cst_exp_list.exp)};
  
append_expr {-> exp} =
  append l_paren cst_exp comma other_exp r_paren { -> New exp.appended_expr(cst_exp.exp, other_exp.exp)};

other_exp {-> exp} =
  cst_exp { -> cst_exp.exp};

factor {-> exp} = 
  {cst_times}       factor star term  {-> New exp.mult(factor.exp,term.exp)} |
  {cst_divide}      factor slash term {-> New exp.divide(factor.exp,term.exp)} |
  {cst_modulo}      factor modulo term {-> New exp.modulo(factor.exp, term.exp)} |
  {cst_shift_left}  factor shift_left term {-> New exp.shift_left(factor.exp, term.exp)} |
  {cst_shift_right} factor shift_right term {-> New exp.shift_right(factor.exp, term.exp)} |
  {cst_ampersand}   factor ampersand term {-> New exp.ampersand(factor.exp, term.exp)} |
  {cst_ampersand_caret} factor ampersand_caret term {-> New exp.ampersand_caret(factor.exp, term.exp)} |
  {term}            term              {-> term.exp};

cst_end_if {-> stmt} =
  {cst_else_if} else cst_if_stmt {-> New stmt.else_if(cst_if_stmt.stmt)} |
  {cst_else}    else cst_block   {-> New stmt.else(cst_block.stmt)};

cst_block {-> stmt} =
  {cst_block} l_brace semicolon cst_stmt_list r_brace {-> New stmt.block([cst_stmt_list.stmt])};

cst_stmt_list {-> stmt*} =
  {cst_stmt_list} cst_stmt_eos* {-> [cst_stmt_eos.stmt]};

cst_stmt_eos {-> stmt} =    
  {cst_stmt_eos}  cst_stmt cst_eos {-> cst_stmt.stmt}; // Add eos

cst_eos {-> stmt} =
  {cst_semicolon} semicolon    {-> New stmt.empty()} |
  {cst_eof}       end_of_file  {-> New stmt.empty()};

cst_switch_stmt {-> stmt} =
  {cst_switch} switch cst_simple_stmt_eol? cst_exp? l_brace semicolon cst_switch_case_clause* r_brace {-> New stmt.switch(cst_simple_stmt_eol.stmt, cst_exp.exp, [cst_switch_case_clause.stmt])};

cst_simple_stmt_eol {-> stmt} =
  {cst_simple_eol} cst_simple_stmt semicolon {-> cst_simple_stmt.stmt};

cst_switch_case_clause {-> stmt} =
  {cst_case_clause} cst_switch_case colon cst_stmt_list {-> New stmt.case(cst_switch_case.exp, [cst_stmt_list.stmt])};

cst_switch_case {-> exp} =
  {cst_case}   case cst_exp_list {-> New exp.case(cst_exp_list.exp)} |
  {cst_switch} default {-> New exp.default()}; 

cst_for_condition {-> exp} =
  {cst_empty}    /* empty */ {-> New exp.empty()} |
  {cst_exp}      cst_exp {-> cst_exp.exp} |
  {cst_clause}   [l]:cst_simple_stmt? [sl]:semicolon cst_exp? [sr]:semicolon [r]:cst_simple_stmt? {-> New exp.for_cond(l.stmt, cst_exp.exp, r.stmt)};

cst_assign_stmt {-> stmt} =
  {cst_assign_stmt} lvalue op_equals cst_exp {-> New stmt.assign_op(lvalue.exp,op_equals,cst_exp.exp)} |
  {cst_assign_op_stmt} cst_assign_op_stmt {-> cst_assign_op_stmt.stmt};

cst_assign_op_stmt {-> stmt} =
  {lvalue_assign} lvalue equals cst_exp {-> New stmt.assign(lvalue.exp, cst_exp.exp)} |
  {lvalue_assign_list} lvalue [l]:comma cst_assign_op_stmt [r]:comma cst_exp {-> New stmt.assign_list(lvalue.exp, cst_assign_op_stmt.stmt, cst_exp.exp)};

cst_short_decl_stmt {-> stmt} =
  {cst_short_decl} id colon_equals cst_exp {-> New stmt.short_decl(id, cst_exp.exp)};
  //{cst_short_decl_list} id [l]:comma cst_short_decl_stmt [r]:comma cst_exp {-> New stmt.short_decl_list(id, cst_short_decl_stmt.stmt, cst_exp.exp)};

cst_id_comma {-> id} =
  {cst_id_comman} id comma {-> id};

cst_exp_list {-> exp} =
  {cst_exp}      cst_exp     {-> cst_exp.exp} |
  {cst_exp_list} cst_exp_list comma cst_exp {-> New exp.list(cst_exp_list.exp, cst_exp.exp)};

cst_id_list {-> exp} =
  {cst_id}       id                   {-> New exp.id(id)} |
  {cst_id_list}  cst_id_list comma id {-> New exp.id_list(cst_id_list.exp, id)};

term {-> exp} =
  {cst_id}        id     {-> New exp.id(id)} |
  {cst_float64}   float64_literal  {-> New exp.float64_literal(float64_literal)} |
  {cst_int}       int    {-> New exp.int(int)} |
  {cst_oct}       oct    {-> New exp.oct(oct)} |
  {cst_hex}       hex    {-> New exp.hex(hex)} |
  {cst_rune_literal} rune_literal {-> New exp.rune_literal(rune_literal)} |
  {cst_raw_string_lit} raw_string_lit {-> New exp.raw_string_lit(raw_string_lit)} |
  {cst_interpreted_string_literal} interpreted_string_literal {-> New exp.interpreted_string_literal(interpreted_string_literal)} |
  {paren}         l_paren cst_exp r_paren  {-> cst_exp.exp};

lvalue {-> exp} =
  {cst_id}      id     {-> New exp.id(id)} |
  {array_index} lvalue cst_index {-> New exp.array_index(lvalue.exp, cst_index.exp)} |
  {struct_selector}  lvalue cst_selector  {-> New exp.struct_selector(lvalue.exp, cst_selector.exp)};

lvalue_no_id {-> exp} =
  {cst_index}      id cst_index     {-> New exp.array_index(New exp.id(id), cst_index.exp)} |
  {cst_selector}   id cst_selector  {-> New exp.struct_selector(New exp.id(id), cst_selector.exp)} |
  {cst_lvalue_index}    lvalue_no_id cst_index    {-> New exp.array_index(lvalue_no_id.exp, cst_index.exp)} |
  {cst_lvalue_selector} lvalue_no_id cst_selector {-> New exp.struct_selector(lvalue_no_id.exp, cst_selector.exp)};

cst_index {-> exp} =
  {cst_index} l_brack cst_exp r_brack {-> cst_exp.exp};

cst_selector {-> exp} =
  {cst_selector} dot cst_exp {-> cst_exp.exp};

Abstract Syntax Tree

program = package_decl decl* stmt*;

decl = 
  {pack_decl_ast} package_decl |
  {func_decl_ast} func_decl |
  {var_decl_ast} var_decl |
  {type_decl_ast} type_decl
  ;

package_decl = 
  id_type
  ;

func_decl = 
  {no_return} id_type signature? [block]:stmt |
  {single_return} id_type signature? var_type [block]:stmt  |
  ;

signature = 
  {single_id_to_type} id_type var_type |
  {many_id_to_type} id_type signature |
  {multiple_types} [root]:signature [leaf]:signature
  ;

type_decl = 
  {type_alias} id_type var_type |
  {type_with_many_ids} id_type type_decl |
  {struct_with_id} id_type type_decl* |
  {multiline_list} type_decl*
  ;

var_decl = 
  {var_with_type} id_type var_type |
  {var_with_only_exp} id_type exp |
  {var_with_type_and_exp} id_type var_type exp |
  {inline_list_no_exp} id_type var_decl |
  {inline_list_with_exp} id_type var_decl exp |
  {multiline_list} var_decl*
  ;

var_type = 
  {base_type} type |
  {slice} var_type |
  {array} int var_type |
  {struct} id
  ;

id_type = 
  {id} id |
  {type} type
  ;

stmt =
  {read}            id |
  {print}           exp? |
  {println}         exp? |
  {return}          exp? |
  {increment}       exp  |
  {decrement}       exp  |
  {decl}            decl |
  {assign}          [l]:exp [r]:exp |
  {assign_op}       [l]:exp op_equals [r]:exp |
  {assign_list}     [l]:exp stmt [r]:exp |
  {short_decl}      id exp |
  {short_decl_list} id stmt exp |
  {block}           stmt* |
  {while}           exp stmt* |
  {if}              exp [block]:stmt [end]:stmt?  |
  {else_if}          stmt |
  {else}            stmt |
  {for}             [condition]:exp [block]:stmt |
  {switch}          [simple_stmt]:stmt? exp? [case_stmts]:stmt* |
  {case}            [case_exp]:exp  [stmt_list]:stmt* |
  {break}     |
  {continue}  | 
  {empty};

exp =
  {plus}   [l]:exp [r]:exp |
  {minus}  [l]:exp [r]:exp |
  {mult}   [l]:exp [r]:exp |
  {divide} [l]:exp [r]:exp |
  {modulo} [l]:exp [r]:exp |
  {ampersand} [l]:exp [r]:exp |
  {ampersand_caret} [l]:exp [r]:exp |
  {shift_left} [l]:exp [r]:exp |
  {shift_right} [l]:exp [r]:exp |
  {logical_or} [l]:exp [r]:exp |
  {logical_and} [l]:exp [r]:exp |
  {equals_equals} [l]:exp [r]:exp |
  {not_equal} [l]:exp [r]:exp |
  {less} [l]:exp [r]:exp |
  {less_equals} [l]:exp [r]:exp |
  {greater_equals} [l]:exp [r]:exp |
  {greater} [l]:exp [r]:exp |
  {pipe} [l]:exp [r]:exp |
  {caret} [l]:exp [r]:exp |
  {id} id |
  {float64_literal} float64_literal |
  {int} int |
  {oct} oct |
  {hex} hex |
  {unary_plus} exp |
  {unary_minus} exp |
  {unary_xor} exp |
  {unary_exclamation} exp |
  {rune_literal} rune_literal |
  {raw_string_lit} raw_string_lit |
  {interpreted_string_literal} interpreted_string_literal |
  {function_call} id exp |
  {appended_expr} [l]:exp [r]:exp |
  {array_index}      [lvalue]:exp [index]:exp |
  {struct_selector}  [l]:exp [r]:exp |
  {list}      [list]:exp [value]:exp |
  {id_list}   [list]:exp [value]:id  |
  {for_cond}  [first]:stmt? [second]:exp? [third]:stmt? |
  {case}      [exp_list]:exp |
  {default} |
  {empty};
